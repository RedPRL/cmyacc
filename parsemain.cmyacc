name ParseMainFun

terminal IDENT of pos_symbol
terminal NUMBER of pos_int
terminal ARROW of pos
terminal COLON of pos
terminal EQUAL of pos
terminal NAME of pos
terminal NONTERMINAL of pos
terminal NOPREC of pos
terminal LPAREN of pos
terminal OF of pos
terminal PRECL of pos
terminal PRECR of pos
terminal RPAREN of pos
terminal START of pos
terminal TERMINAL of pos

nonterminal Ident : symbol =
   1:IDENT => sole_ident

nonterminal Number : int =
   1:NUMBER => sole_number

nonterminal Label : label =
   1:Ident => ident_label
   1:Number => number_label

nonterminal Constituent : constituent =
   1:Ident => unlabeled_item
   1:Label COLON 2:Ident => labeled_item
   LPAREN 1:Constituent RPAREN => paren_item

nonterminal Constituents : constituents =
   => nil_constituents
   1:Constituent 2:Constituents => cons_constituents

nonterminal Precedence : precedence =
   => empty_precedence
   PRECL 1:Number => left_precedence
   PRECR 1:Number => right_precedence
   NOPREC => no_precedence

nonterminal Production : production =
   1:Constituents ARROW 2:Ident 3:Precedence => sole_production

nonterminal Productions : productions =
   => nil_productions
   1:Production 2:Productions => cons_productions

nonterminal Directive : directive =
   NAME 1:Ident => name_directive
   TERMINAL 1:Ident 2:Precedence => terminal_directive
   TERMINAL 1:Ident OF 2:Ident 3:Precedence => terminal_of_directive
   NONTERMINAL 1:Ident COLON 2:Ident EQUAL 3:Productions => nonterminal_directive
   START 1:Ident => start_directive

nonterminal Directives : directives =
   => nil_directives
   1:Directive 2:Directives => cons_directives

start Directives
