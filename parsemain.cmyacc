name ParseMainFun

terminal IDENT of pos_symbol
terminal ARROW of pos
terminal COLON of pos
terminal EQUAL of pos
terminal NAME of pos
terminal NONTERMINAL of pos
terminal LPAREN of pos
terminal OF of pos
terminal RPAREN of pos
terminal START of pos
terminal TERMINAL of pos

nonterminal Ident : ident =
   ident:IDENT => ident

nonterminal Constituent : constituent =
   (ident:Ident) => unlabeled_item
   (label:Ident) COLON (ident:Ident) => labeled_item
   LPAREN (constituent:Constituent) RPAREN => paren_item

nonterminal Constituents : constituents =
   => nil_constituents
   (head:Constituent) (tail:Constituents) => cons_constituents

nonterminal Productions : productions =
   => nil_productions
   (constituents:Constituents) ARROW (action:Ident) (tail:Productions) => cons_productions

nonterminal Directive : directive =
   NAME (ident:Ident) => name_directive
   TERMINAL (ident:Ident) => terminal_directive
   TERMINAL (ident:Ident) OF (tp:Ident) => terminalOf_directive
   NONTERMINAL (ident:Ident) COLON (tp:Ident) EQUAL (arms:Productions) => nonterminal_directive
   START (ident:Ident) => start_directive

nonterminal Directives : directives =
   => nil_directives
   (head:Directive) (tail:Directives) => cons_directives

start Directives
